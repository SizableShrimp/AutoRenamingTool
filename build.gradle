buildscript {
    repositories {
        mavenCentral()
        maven { url = 'https://maven.neoforged.net/' }
    }
    dependencies {
        classpath  group: 'net.neoforged.gradleutils', name: 'GradleUtils', version: '[2.0.18,)', changing: true
    }
}

plugins {
    id 'eclipse'
    id 'signing'
    id 'java-library'
    id 'maven-publish'
    id 'org.cadixdev.licenser' version '0.6.1'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'io.github.gradle-nexus.publish-plugin' version '1.3.0'
}
apply plugin: 'net.neoforged.gradleutils'

group 'net.neoforged'
version = gradleutils.getTagOffsetBranchVersion(null, 'main', 'HEAD')
println('Version: ' + version)

java.toolchain.languageVersion = JavaLanguageVersion.of(8)
compileJava.options.encoding = 'UTF-8'

ext {
    CHANGELOG = rootProject.file('build/changelog.txt')
    REPO = 'AutoRenamingTool'
    TITLE = 'Auto Renaming Tool'
    MANIFEST = manifest{
        attributes('Main-Class': 'net.minecraftforge.fart.Main')
        attributes([
            'Specification-Title':      'AutoRenamingTool',
            'Specification-Vendor':     'Neo Forged',
            'Specification-Version':    gradleutils.gitInfo.tag,
            'Implementation-Title':     'ART',
            'Implementation-Version':   project.version,
            'Implementation-Vendor':    'NeoForged'
        ] as LinkedHashMap, 'net/minecraftforge/fart/')
    }
    INITIAL_CHANGELOG_TAG = '1.0'
}

repositories {
    mavenCentral()
    maven { url = 'https://maven.neoforged.net/' }
}

test {
    useJUnitPlatform()
}

license {
    header = file('LICENSE-header.txt')
    ext.project = TITLE
}

dependencies {
    implementation 'net.sf.jopt-simple:jopt-simple:6.0-alpha-3'
    api 'net.minecraftforge:srgutils:0.5.1'
    implementation 'org.ow2.asm:asm:9.5'
    implementation 'org.ow2.asm:asm-commons:9.5'
    implementation 'org.ow2.asm:asm-tree:9.5'
    implementation 'net.neoforged.javadoctor:gson-io:2.0.0'

    testImplementation(platform('org.junit:junit-bom:5.8.1'))
    testImplementation('org.junit.jupiter:junit-jupiter')
    testImplementation('org.powermock:powermock-core:2.0.9')

    compileOnly 'org.jetbrains:annotations:24.0.1'
}


test {
  useJUnitPlatform()
  testLogging {
    events 'passed', 'skipped', 'failed'
  }
}

java.withSourcesJar()
java.withJavadocJar()

shadowJar {
    manifest.from(MANIFEST)
    minimize()

    def relocations = [
            'org.objectweb.asm',
            'net.minecraftforge.srgutils',
            'joptsimple'
    ]

    relocations.forEach {
        relocate it, "net.minecraftforge.fart.relocated.$it"
    }
}

assemble.dependsOn shadowJar

nexusPublishing {
    repositories {
        sonatype {
            username.set(System.getenv('SONATYPE_USER') ?: '')
            password.set(System.getenv('SONATYPE_PASSWORD') ?: '')
            nexusUrl.set(uri('https://s01.oss.sonatype.org/service/local/'))
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = TITLE
                description = 'A tool that renames java bytecode elements.'
                url = "https://github.com/neoforged/${REPO}"
                scm {
                    url = "https://github.com/neoforged/${REPO}"
                    connection = "scm:git:git://github.com/neoforged/${REPO}.git"
                    developerConnection = "scm:git:git@github.com:neoforged/${REPO}.git"
                }
                issueManagement {
                    system = 'github'
                    url = "https://github.com/neoforged/${REPO}/issues"
                }

                licenses {
                    license {
                        name = 'LGPLv2.1'
                        url = 'https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt'
                    }
                }

                developers {
                    developer {
                        id = 'neoforged'
                        name = 'NeoForged'
                        email = 'contact@neoforged.net'
                        url = 'https://github.com/NeoForged/'
                    }
                }
            }
        }
    }
    repositories {
        maven gradleutils.getPublishingForgeMaven()
    }
}

if (System.getenv('GPG_PRIVATE_KEY')) {
    signing {
        final signingKey = System.getenv('GPG_PRIVATE_KEY') ?: ''
        final signingPassword = System.getenv('GPG_KEY_PASSWORD') ?: ''
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.mavenJava
    }
}

changelog {
    fromTag INITIAL_CHANGELOG_TAG
}